class Game {

    // Board is [rows][cols]
    static Array board;
    static int boardCols;
    static int boardRows;

    static int rowsComplete;

    static int countdown;
    static int previousKey;

    static Cursor trialCursor;
    static Cursor nextCursor;
    static Cursor cursor;

    static int GAME_OVER;
    static int RUNNING;
    static int TITLE_SCREEN;
    static int gameState;

    static int BOARD_OFFSET_X;
    static int BOARD_OFFSET_Y;

    function void init() {

        do Graphics.init();
        do Cursor.init();

        let trialCursor = Cursor.new(0, 0, 0, 0);
        let nextCursor = Cursor.new(0, 0, 0, 0) ;
        let cursor = Cursor.new(0, 0, 0, 0);

        let boardRows = 18;
        let boardCols = 10;
        let board = Game.createBoard(boardRows, boardCols);

        let GAME_OVER = 3;
        let RUNNING = 4;
        let TITLE_SCREEN = 5;
        let gameState = TITLE_SCREEN;

        let BOARD_OFFSET_X = 2;
        let BOARD_OFFSET_Y = 0;

        do Graphics.drawGBFrame();
        do Game.enterGameLoop();
        return;
    }

    function void enterGameLoop(){
        var bool stateChanged;
        var int key;

        while(true){

            if(gameState = TITLE_SCREEN){
                do Graphics.clearGBScreen();
                do Output.moveCursor(10, 10);
                do Output.printString("Press a key");

                while(Keyboard.keyPressed() = 0){
                    // TODO: Set seed by counting cycles on title screen until input.
                    do LCGRandom.setSeed(999);
                }

                while(~(Keyboard.keyPressed() = 0)){}
                let gameState = RUNNING;
            }

            if(gameState = RUNNING){
                let rowsComplete = 0;

                let cursor = Game.randomizeCursor(cursor);
                let nextCursor = Game.randomizeCursor(nextCursor);

                // TODO: This happens in two places!
                do cursor.setX(4);
                do cursor.setY(0);

                do Game.clearBoard(board, boardRows, boardCols);

                let previousKey = 0;

                while(gameState = RUNNING){
                    let stateChanged = Game.update();
                    if(stateChanged){
                        do Graphics.clearGBScreen();
                        do Game.render();
                    }
                }
            }

            if(gameState = GAME_OVER){
                do Graphics.clearGBScreen();
                do Output.moveCursor(10, 10);
                do Output.printString("Game over");

                while(Keyboard.keyPressed() = 0){}
                while(~(Keyboard.keyPressed() = 0)){}
                let gameState = TITLE_SCREEN;
            }
        }
        return;
    }

    function Cursor randomizeCursor(Cursor aCursor){
        do aCursor.setRotation(LCGRandom.randRange(0, 3));
        do aCursor.setTetronimo(LCGRandom.randRange(0, 6));
        return aCursor;
    }

    function bool update(){
        var int key, dx, dy, dr, newRotation, possibleRotations;
        var bool stateChanged;
        var Cursor temp;

        let key = Keyboard.keyPressed();
        if(~(key = previousKey)){
            let previousKey = key;

            let dx = 0;
            let dr = 0;

            if(key = 130){
                let dx = -1;
            }

            if(key = 132){
                let dx = 1;
            }

            if(key = 131){ // Up
                let dr = 1;
            }
        }

        if(countdown = 0){
            let dy = 1;
        } else {
            let dy = 0;
        }

        let stateChanged = false;

        if(~(dx=0) | ~(dr=0)){
            if(Game.isValidPosition(cursor, dx, 0, dr)){
                let stateChanged = true;
                do cursor.setX(cursor.getX() + dx);
                do cursor.rotate(dr);
            }
        }

        // Handle vertical movement last and alone - triggers new blocks
        if(~(dy=0)){
            if(Game.isValidPosition(cursor, 0, dy, 0)){
                let stateChanged = true;
                do cursor.setY(cursor.getY() + dy);

            } else {

                do Game.writeToBoard(cursor, board);
                do Game.removeCompleteLines();

                let temp = cursor;
                let cursor = nextCursor;
                let nextCursor = Game.randomizeCursor(temp);
                let temp = null;

                // Position the cursor at the top of the screen
                do cursor.setX(3);
                do cursor.setY(0);

                if(~Game.isValidPosition(cursor, 0, 0, 0)){
                    let gameState = GAME_OVER;
                }
            }
        }

        if(countdown = 0){
            let countdown = 4000;
        } else {
            let countdown = countdown -1;
        }
        return stateChanged;
    }

    function void render(){
        do Graphics.drawGameScreen();
        do Game.renderBoard();
        do Game.renderShape(cursor);
        do Game.renderNextShape(nextCursor);
        return;
    }

    function void renderBoard(){
        var int boardY, boardX, row;
        var Array tileBMP;

        let boardY = 0;
        while(boardY<boardRows){
            let boardX = 0;
            let row = board[boardY];
            while(boardX<boardCols){
                if(~(row[boardX] = 0)){
                    let tileBMP = Graphics.getTetronimoTile(row[boardX]);
                    do Graphics.drawGBTile(tileBMP, BOARD_OFFSET_X + boardX, BOARD_OFFSET_Y + boardY);
                }
                let boardX = boardX+1;
            }
            let boardY = boardY+1;
        }
        return;
    }

    function void renderShape(Cursor aCursor){
        var int boardX, tileX, cX,
                boardY, tileY, cY,
                tile;
        var Array cTiles, tileBMP;

        let cX = cursor.getX();
        let cY = cursor.getY();
        let cTiles = cursor.getTiles();

        let tileY = 0;
        let boardY = cY + tileY;

        while(tileY<4){
            let tileX = 0;
            let boardX = cX + tileX;
            while(tileX<4){
                let tile = Matrix.get(cTiles, tileY, tileX);
                if(~(tile = 0)){
                    let tileBMP = Graphics.getTetronimoTile(tile);
                    do Graphics.drawGBTile(tileBMP, BOARD_OFFSET_X + boardX, BOARD_OFFSET_Y + boardY);
                }
                let tileX = tileX+1;
                let boardX = cX + tileX;
            }
            let tileY = tileY+1;
            let boardY = cY + tileY;
        }
        return;
    }

    function void renderNextShape(Cursor aCursor){
        var int screenX, tileX, cX,
                screenY, tileY, cY,
                tile, tileBMP;
        var Array cTiles;

        let cX = 15;
        let cY = 13;

        let cTiles = aCursor.getTiles();

        let tileY = 0;
        let screenY = cY + tileY;

        while(tileY<4){
            let tileX = 0;
            let screenX = cX + tileX;
            while(tileX<4){
                let tile = Matrix.get(cTiles, tileY, tileX);
                let tileBMP = Graphics.getTetronimoTile(tile);
                do Graphics.drawGBTile(tileBMP, screenX, screenY);
                let tileX = tileX+1;
                let screenX = cX + tileX;
            }
            let tileY = tileY+1;
            let screenY = cY + tileY;
        }
        return;
    }

    function Array createBoard(int rows, int columns){
        var Array board, row;
        var int i, j;

        let board = Array.new(rows);
        let i = 0;

        while(i<rows){
            let row = Array.new(columns);
            let board[i] = row;
            let j = 0;
            while(j<columns){
                let row[j] = 0;
                let j = j+1;
            }
            let i = i+1;
        }
        return board;
    }

    function void clearBoard(Array aBoard, int rows, int columns){
        var Array row;
        var int i, j;

        let i = 0;
        while(i<rows){
            let row = board[i];
            let j = 0;
            while(j<columns){
                let row[j] = 0;
                let j = j+1;
            }
            let i = i+1;
        }
        return;
    }

    function bool rowComplete(Array row){
        var int i, filledCount;

        let i = 0;
        let filledCount = 0;

        while(i<boardCols){
            if(row[i] > 0){
                let filledCount = filledCount + 1;
            }
            let i = i + 1;
        }
        return (filledCount = boardCols);
    }

    function void removeCompleteLines(){
        var int i, j, k;
        var Array newRow, oldRow;

        let i = boardRows - 1;

        while(i>-1){

            while(Game.rowComplete(board[i])){
                let rowsComplete = rowsComplete + 1;

                let oldRow = board[i];
                do oldRow.dispose();
                let board[i] = null;

                let j = i;
                while(j>-1){
                    if(j>0){
                        let board[j] = board[j-1];
                        let board[j-1] = null;

                    } else {
                        let newRow = Array.new(boardRows);
                        let k = 0;
                        while(k<boardRows){
                            let newRow[k] = 0;
                            let k = k+1;
                        }
                        let board[j] = newRow;
                    }
                    let j = j - 1;
                }
            }
            let i = i - 1;
        }
        return;
    }

    /**
     * Is the cursor position valid, optionally with an offset to
     * decide for candidate moves.
     *
     * A position is invalid if the tentronimo overlaps existing pieces
     * or is off the board.
     */
    function bool isValidPosition(Cursor aCursor, int dX, int dY, int dr){
        var int boardX, boardY, tileX, tileY, boardBaseX, boardBaseY, boardVal, tileVal;
        var Array cTiles;

        // Match cursor props.
        do trialCursor.setTetronimo(aCursor.getTetronimo());
        do trialCursor.setX(aCursor.getX());
        do trialCursor.setY(aCursor.getY());
        do trialCursor.setRotation(aCursor.getRotation());

        // Make the dx/dy/dr adjustments
        do trialCursor.setX(trialCursor.getX() + dX);
        do trialCursor.setY(trialCursor.getY() + dY);
        do trialCursor.rotate(dr);

        // Do the rest with the 'trial' cursor
        let boardBaseX = trialCursor.getX();
        let boardBaseY = trialCursor.getY();
        let cTiles = trialCursor.getTiles();

        let tileY = 0;
        while(tileY < 4){
            let tileX = 0;
            while(tileX < 4){
                let boardX = boardBaseX + tileX;
                let boardY = boardBaseY + tileY;

                let tileVal = Matrix.get(cTiles, tileY, tileX);
                if((boardX>-1) & (boardX<boardCols) & (boardY>-1) & (boardY<boardRows)){
                    let boardVal = Matrix.get(board, boardY, boardX);
                } else {
                    let boardVal = -1;
                }

                if((tileVal > 0) & (boardVal > 0)){
                    return false; // Overlapping cells
                }

                if((tileVal > 0) & (boardVal = -1)) {
                    return false; // Cells off-the board
                }
                let tileX = tileX + 1;
            }
            let tileY = tileY + 1;
        }
        return true;
    }

    function void writeToBoard(Cursor aCursor, Array aBoard){
        var int boardX, boardY, tileX, tileY, boardBaseX, boardBaseY, boardVal, tileVal;
        var Array cTiles;

        let boardBaseX = aCursor.getX();
        let boardBaseY = aCursor.getY();
        let cTiles = aCursor.getTiles();

        let tileY = 0;
        while(tileY < 4){
            let tileX = 0;
            while(tileX < 4){
                let boardX = boardBaseX + tileX;
                let boardY = boardBaseY + tileY;

                if((boardX>-1) & (boardX<boardCols) & (boardY>-1) & (boardY<boardRows)){
                    let tileVal = Matrix.get(cTiles, tileY, tileX);
                    if(tileVal > 0){  // Don't copy empty!
                        do Matrix.set(aBoard, boardY, boardX, tileVal);
                    }
                }
                let tileX = tileX + 1;
            }
            let tileY = tileY + 1;
        }
        return;
    }
}
